grammar de.cau.cs.kieler.sim.coreeso.CoreEso with org.eclipse.xtext.common.Terminals

generate coreEso "http://www.cau.de/cs/kieler/sim/coreeso/CoreEso"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

coreTracelist:
    (traces+=coreTrace)+;

coreTrace:
    '!' 'reset' ';'
    (ticks+=coreTick)+;

// !!! Es geht die Information über  pure signale bzgl valued signals verloren
// denn: später ist: A => A : true und A'(true) => A' : true und A'_present : true/false
// A und A' sind nicht mehr zu trennen. A wird nach dem tick zu false und A' behät seinen Wert
// nur A'_present wird nach dem tick false

coreTick:
    {coreTick}
    (input+=coreKvpair)*
    ('%' 'Output' ':' (output+=coreKvpair)*)?
    ';';

/* From the Xtext documentation:
 * The ?= sign (boolean assignment operator) expects a feature of type EBoolean and sets it to true if
 * the right hand side was consumed independently from the concrete value of the right hand side.
 * 
 * I. e. 'valued' is set to true if a value could be read.
 */
coreKvpair:
    {coreKvpair}
    '%%'
    key=(ALPHANUMSPECIAL)
    ':'
    value=(CoreEsoJson | CoreEsoString | CoreEsoFloat | CoreEsoBool | CoreEsoInt);
    
CoreEsoInt:
    value = INT
;

CoreEsoString:
    value = STRING
;

CoreEsoFloat:
    value = FLOAT
;

CoreEsoBool:
    value = BOOL
;

CoreEsoJson:
    value = ALPHANUMSPECIAL
;

terminal SPECIAL:
    ',' | '.' | '/' | '@' | '#' | '$' | '&' | '*' | '=' | '+' | '-' | '_';
    
terminal BOOL returns ecore::EBoolean:
    ('true' | 'false');
    
terminal FLOAT returns ecore::EFloat:
    ('+' | '-')? ('0'..'9')+ '.' ('0'..'9')+;

terminal INT returns ecore::EInt: ('0'..'9')+;

terminal ALPHANUMSPECIAL:
    ('0'..'9')* ('a'..'z' | 'A'..'Z' | SPECIAL) ('a'..'z' | 'A'..'Z' | '0'..'9' | SPECIAL)*
;

terminal STRING : 
            '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
            "'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
        ;
        
terminal WS         : (' '|'\t'|'\r'|'\n')+;
