/*
 * generated by Xtext
 */
package de.cau.cs.kieler.kvis.ui.contentassist

import com.google.inject.Inject
import de.cau.cs.kieler.kvis.kvis.Animation
import de.cau.cs.kieler.prom.ExtensionLookupUtil
import de.cau.cs.kieler.prom.build.AttributeExtensions
import de.cau.cs.kieler.simulation.core.DataHandler
import org.eclipse.core.runtime.IConfigurationElement
import org.eclipse.emf.ecore.EObject
import org.eclipse.xtext.Assignment
import org.eclipse.xtext.ui.editor.contentassist.ContentAssistContext
import org.eclipse.xtext.ui.editor.contentassist.ICompletionProposalAcceptor
import de.cau.cs.kieler.kvis.ui.animations.AnimationHandler

/** 
 * See https://www.eclipse.org/Xtext/documentation/304_ide_concepts.html#content-assist
 * on how to customize the content assistant.
 */
class KVisProposalProvider extends AbstractKVisProposalProvider {
    @Inject
    extension AttributeExtensions attributeExtensions
    
    override completeAttributeMapping_Attribute(EObject model, Assignment assignment, ContentAssistContext context, ICompletionProposalAcceptor acceptor) {
        // Add proposals of super class
        super.completeAttributeMapping_Attribute(model, assignment, context, acceptor);
        
        // Add proposals for configurable attributes
        val proposals = <String> newArrayList
        switch(model) {
            Animation : {
                // Load the animation handler with the corresponding name
                // to add all its configurable attributes
                val name = model.type
                val requiredConfig = [IConfigurationElement elem | elem.getAttribute("name") == name]
                val configurationElements = ExtensionLookupUtil.getConfigurationElements("de.cau.cs.kieler.kvis.animationHandler",
                                                                                          requiredConfig)
                if(!configurationElements.isNullOrEmpty) {
                    val element = configurationElements.get(0)
                    val handler = ExtensionLookupUtil.instantiateClassFromConfiguration(element) as AnimationHandler
                    if(handler != null) {
                        val attributeNames = handler.configurableAttributes.map[it.name]
                        proposals.addAll(attributeNames)
                    }
                }
            }
        }
        
        // Create and register the completion proposal
        for(proposal : proposals) {
            acceptor.accept(createCompletionProposal(proposal, context))
        }
    }
}
