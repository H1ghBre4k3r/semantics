grammar de.cau.cs.kieler.simulation.trace.KTrace with de.cau.cs.kieler.kexpressions.kext.KExt

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/annotations" as annotations
import "http://kieler.cs.cau.de/kext/0.1.0" as kext
import "http://kieler.cs.cau.de/simulation/ktrace" as ktrace

TraceFile returns ktrace::TraceFile:
    EsoTraces | KTraces
;

// -- ESO --

EsoTraces returns ktrace::TraceFile:
    (traces+=EsoTrace)+
;
    
EsoTrace returns ktrace::Trace:
    '!' 'reset' ';'
    (ticks+=EsoTick)+
;

EsoTick returns ktrace::Tick:
    {ktrace::Tick}
    (inputs+=Emission)*
    ('%' (('Output:')|('Output' ':')) (outputs+=Emission)*)?
    (annotations+=KVPair)*
    ('%%' (('Output:')|('Output' ':')) (annotations+=KVPair)*)?
    ';'
;

KVPair returns annotations::StringAnnotation:
    '%%' name=ID '=' values+=EStringAllTypes
;

// -- KTrace --

KTraces returns ktrace::TraceFile:
    pragmas+=Pragma*
    ('reset' ';')?
    (traces+=KTrace)
    ('reset' ';' traces+=KTrace)*
    ('reset' ';')?
;
    
KTrace returns ktrace::Trace:
    pragmas+=Pragma*
    (ticks+=KTick)+;

KTick returns ktrace::Tick:
    {ktrace::Tick}
    (inputs+=Assignment)*
    ('/' (outputs+=Assignment)*)?
    'pause'? ';'
;    