grammar de.cau.cs.kieler.kvis.KVis with de.cau.cs.kieler.kexpressions.KExpressions

generate kvis "http://www.cau.de/cs/kieler/kvis/KVis"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/kexpressions/0.1.2" as kexpressions 

Visualization:
	'image' ':' image=STRING
	elements += Element*
;


Element:
	'animate' name=ID '{'
        (animations+=Animation)+
	'}'
;

Animation returns Animation:
    'apply' type=ID ('using' variable=VariableReference)? '{'
        (attributeMappings+=AttributeMapping)*
    '}' ('when' condition=BoolExpression)?
;

AttributeMapping:
    attribute=ID ':' (literal=Literal | (mappings+=Mapping) (',' mappings+=Mapping)*)
;

Mapping:
    variableDomain = VariableDomain 'is' attributeDomain = AttributeDomain
;

VariableDomain returns Domain:
    value = Literal
    | range = Interval
;

Interval:
    (from=Integer | '('from=Integer')')
    Range
    (to=Integer | '('to=Integer')')
;

Range:
    '-'
;

AttributeDomain returns Domain:
    VariableDomain
;

VariableReference:
    (model=ModelReference)?
    name=ID
    ('[' indices+=INT ']')*
;

ModelReference:
    name=ID '.'
;

Literal:
    Integer | Floateger | STRING | BOOLEAN
;