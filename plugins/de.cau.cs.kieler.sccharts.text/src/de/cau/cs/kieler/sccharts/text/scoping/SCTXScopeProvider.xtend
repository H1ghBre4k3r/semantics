/*
 * generated by Xtext
 */
package de.cau.cs.kieler.sccharts.text.scoping

import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EReference
import de.cau.cs.kieler.sccharts.Transition
import org.eclipse.xtext.scoping.IScope
import com.google.inject.Inject
import de.cau.cs.kieler.sccharts.extensions.SCChartsExtension
import de.cau.cs.kieler.sccharts.State
import de.cau.cs.kieler.sccharts.ControlflowRegion
import de.cau.cs.kieler.kexpressions.KExpressionsPackage
import de.cau.cs.kieler.kexpressions.ReferenceDeclaration
import de.cau.cs.kieler.sccharts.Scope
import de.cau.cs.kieler.annotations.PragmaStringAnnotation

/**
 * This class contains custom scoping description.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#scoping
 * on how and when to use it.
 *
 */
class SCTXScopeProvider extends de.cau.cs.kieler.kexpressions.kext.scoping.KExtScopeProvider {
    
    @Inject extension SCChartsExtension
    
    @Inject SCTXQualifiedNameProvider nameProvider

    override getScope(EObject context, EReference reference) {
//        println(context + "\n  " + reference)
        if (context instanceof Transition) {
            return getScopeForTransition(context, reference)
        }
        
        return super.getScope(context, reference);
    }
    
    protected def IScope getScopeForTransition(Transition transition, EReference reference) {
        val states = <State> newArrayList
        val parentState = transition.eContainer as State
        val parentRegion = parentState.eContainer as ControlflowRegion
        
        parentRegion.states.forEach[ 
            states += it 
        ]
        
        return SCTScopes.scopeFor(states)
    }
    
    override def IScope getScopeForReferenceDeclaration(EObject context, EReference reference) {
        if (reference == KExpressionsPackage.Literals.REFERENCE_DECLARATION__REFERENCE) {
            val candidates = <Scope> newArrayList 
            
            val declaration = context
            if (declaration instanceof ReferenceDeclaration) {
                val superScope = super.getScope(context.eContainer, reference)
                val root = context.root.asSCCharts
                candidates += root.rootStates
                val imports = root.annotations.filter(PragmaStringAnnotation).filter[ name.equals("import") ].toList
                
                val res = context.eResource      
                if (res != null) {
                    val resSet = res.resourceSet
                    
                    if (resSet != null) {
                        val resSetFilter = resSet.resources.filter[ 
                            val uri = it.URI.toPlatformString(true)
//                            println(uri)
                            return imports.exists[ uri.endsWith(it.values.head) ]
                        ]
                        for(r : resSetFilter) {
                            candidates += r.contents.filter[ it instanceof State ].map[ it as Scope ]
                        }
                    }
                }      
                
                return SCTScopes.scopeFor(candidates, nameProvider, superScope)
            }
        }
        return context.getScopeHierarchical(reference)
    }       

}
